title: Choosing Our Enemies
new-page: true
view-if: year <= 2025 and enemies_timer <= 0
on-arrival: month_actions += 1; enemies_timer = 15
max-visits: 5
is-card: true
card-image: img/poster_1.png
tags: party_affairs

= Choosing our enemies

In our media and campaign materials, who should we consider as our main opponents?

[? if enemies != 0 : Previously, we had considered the [+ enemies +] as our prior enemies. Is a change of direction necessary? ?]

- @anti_dem: The anti-democratic parties of the far right and left - [? if not bsw_formed: Left and AfD. ?][? if bsw_formed: Left, BSW, and AfD. ?]
- @esoteric: The esoteric parties - BSW and AfD.
- @afd: The AfD alone.
- @linke: The Left alone.
- @bourgeois: The bourgeois parties - the CDU and FDP.
- @easy_discard

@anti_dem
on-arrival: enemies = "anti-democratic parties"; neoliberal_strength += 5; seeheimer_strength += 5; left_dissent += 5; workers_linke -= 2; workers_afd -= 2; workers_bsw -= 2; linke_relation -= 10; bsw_relation -= 10 if bsw_formed

Attacking the far right and left is popular among all but the left wing of our party, who would prefer to be fighting the bourgeois parties. This might also hamper our ability to reconcile with the Left Party in the future.

@esoteric
view-if: bsw_formed
on-arrival: enemies = "esotericists"; workers_afd -= 3; workers_bsw -= 3;

Attacking the esotericists is popular among the entire party, although the left considers the bourgeois parties to be just as dangerous, while the our bourgeois coalition partners are apathetic to our actions.

@afd
on-arrival: enemies = "AfD"; modernist_strength += 5; workers_afd -= 4; unemployed_afd -= 3 if nazi_urgency > 1
on-departure: nazi_urgency += 1

[? if nazi_urgency == 0 or afd_r < 15 : The general consensus is that the AfD is a fringe party similar to the NPD which will lose popular support with time. Many in our party are questioning our choices. ?][? if nazi_urgency > 0 and afd_r >= 15 : The neo-Nazis are the most urgent problem for us to deal with. ?]

@linke
on-arrival: enemies = "Left"; seeheimer_strength += 5; left_dissent += 8; seeheimer_dissent -= 5; linke_relation -= 15; fdp_relation += 5; cdu_relation += 5; workers_linke -= 4;

Attacking the Left Party is supported by all but the most left-wing socialists, and wins us some measure of approval among the bourgeois parties. However, if we are to reconcile with the Left Party, this may not be the best choice.

@bourgeois
on-arrival: enemies = "bourgeois parties"; left_strength += 8; left_dissent -= 5; fdp_relation -= 8; cdu_relation -= 8; linke_relation += 5; seeheimer_dissent += 8; workers_cdu -= 3; coalition_dissent += 1

Our anti-bourgeois rhetoric is popular among the left, but may make it harder to build the coalitions that are necessary for government in the future.
