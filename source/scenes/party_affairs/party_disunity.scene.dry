title: Party Disunity
new-page: true
is-card: true
view-if: party_disunity_timer = 0 and (dissent >= 0.3 or (seeheimer_dissent > 30 and not reformists_resign) or (neoliberal_dissent > 30 and not centrists_resign) or (left_dissent > 30 and not sapd_formed) or (labor_dissent > 30 and not unions_independent) or (neorevisionist_dissent > 30 and neorevisionism))
on-arrival: month_actions += 1; party_disunity_timer = 4
card-image: img/image0.jpg
frequency: 200
tags: party_affairs

# TODO: party dissent events for every faction, have a good image for this... 

= Party Disunity

The party is being torn apart by internal dissent. Upset at the leadership and general direction, factions have started sabotaging their rivals, hampering our efforts to win votes and carry out our policies.

[? if seeheimer_dissent > 30 : The Seeheimers are upset at our tolerance of radicalism.  ?]
[? if neoliberal_dissent > 30 : The neoliberals are displeased with the direction of our party. ?]
[? if labor_dissent > 30 : The trade unions disagree with our present course of action. ?]
[? if left_dissent > 30: Leftists are making noises about another party split. ?]
[? if neorevisionist_dissent > 30 and neorevisionism = 1 : Neorevisionists are in disagreement about our political direction. ?]

Something must be done to reinforce party unity.

- @enforce_unity
- @concessions_seeheimers
- @concessions_neoliberal
- @concessions_left
- @concessions_labor
- @concessions_neorevisionists
- @do_nothing


@enforce_unity
title: Enforce party discipline.
on-arrival: seeheimer_dissent -= 5; neoliberal_dissent -= 5; labor_dissent -= 5; left_dissent -= 5; neorevisionist_dissent -= 5; workers_spd -= 4; new_middle_spd -= 2; unemployed_spd -= 2

Enforcing party discipline reduces dissent, but it could lead to an exit of dissenting members.

# enforcing party discipline applies to all parties.

# each party has a concessions action and an expulsions action.

@concessions_seeheimers
title: Give concessions to the reformists.
view-if: seeheimer_dissent > 30
on-arrival: seeheimer_dissent -= 7; left_dissent += 5

We have promised to adopt a more reformist posture. This may alienate the left, however.

@concessions_neoliberal
title: Give concessions to the centrists.
view-if: neoliberal_dissent > 30
on-arrival: neoliberal_dissent -= 7; seeheimer_dissent += 2; left_dissent += 2; neorevisionist_dissent += 2

We have promised to take more policy suggestions from the neoliberals. Both the left and labor wings of the party might be displeased.

@concessions_left
title: Give concessions to the left.
view-if: left_dissent > 30
on-arrival: left_dissent -= 7; seeheimer_dissent += 5

We have promised that we will listen to the left wing of the party. Of course, this leads to increased dissent from the centrists.

@concessions_labor
title: Give concessions to labor.
view-if: labor_dissent > 30
on-arrival: labor_dissent -= 7; neoliberal_dissent += 5; seeheimer_dissent += 5

The trade unions will be granted more influence in party decision making. This upsets the neoliberals and Seeheimers, who would prefer to centralize political decision making within the party.

@concessions_neorevisionists
title: Give concessions to the neorevisionists.
view-if: neorevisionist_dissent > 30 and neorevisionism = 1
on-arrival: neorevisionist_dissent -= 7; seeheimer_dissent += 5;

Giving the neorevisionists more influence will cause increased dissent from the Seeheimers.


@do_nothing
title: Let the factions argue with themselves.
on-arrival: labor_dissent += 1; left_dissent += 1; neoliberal_dissent += 1; seeheimer_dissent += 1; neorevisionist_dissent += 1 if neorevisionism = 1

Let the factions bicker; perhaps dissent is healthy for the party.

# TODO: should we be allowed to preemptively expel factions?

@expel_seeheimers
title: Expel the dissident reformists.
view-if: seeheimer_dissent > 30

@expel_left
title: Expel the dissident leftists.
view-if: left_dissent > 30

@expel_neoliberal
title: Expel the centrists.
view-if: neoliberal_dissent > 30

@expel_labor
title: Expel the labor supporters.
view-if: labor_dissent > 30
